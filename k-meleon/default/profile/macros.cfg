# MACROS DEFINITION FILE (http://kmeleon.sourceforge.net/wiki/index.php?id=MacroLanguage)


# ----ENGINES---- 

Google{
&OldSearch; $search = "Google";
$eng = "http://www.google.com/search?q="; 
&SetSearch;
}

Yahoo{
&OldSearch; $search = "Yahoo";
$eng = "http://search.yahoo.com/bin/search?p=";
&SetSearch;
}

Wikipedia{
&OldSearch; $search = "Wikipedia"; 
$eng = "http://en.wikipedia.org/wiki/";
&SetSearch;
}

Dictionaries{
&OldSearch; $search = "Dictionaries (One Look)";
$eng = "http://www.onelook.com/?w=";
&SetSearch;
}

KM{
&OldSearch; $search = "K-Meleon Forums"; 
$eng = "http://kmeleon.sourceforge.net/forum/search.php?f=3&globalsearch=1&match=1&date=0&fldsubject=1&fldbody=1&search="; 
&SetSearch;
}

URL{
&OldSearch; $search = "URL"; $eng = ""; &Locked;
}


EngineSync{
setcheck("macros(Google)", getpref(STRING, $sen) == "Google" ? true:false);
setcheck("macros(Yahoo)", getpref(STRING, $sen) == "Yahoo" ? true:false);
setcheck("macros(Wikipedia)", getpref(STRING, $sen) == "Wikipedia" ? true:false);
setcheck("macros(Dictionaries)", getpref(STRING, $sen) == "Dictionaries (One Look)" ? true:false);
}


# ----SEARCH MACROS----

$se="kmeleon.general.searchEngine";
$sl="kmeleon.general.searchLocked";
$sen="kmeleon.general.searchEngineName";
$so="kmeleon.general.searchOpen";
$eng=getpref(STRING, $se) ;
$search=getpref(STRING, $sen);
$layers="kmeleon.plugins.layers.load";
$layer=getpref(BOOL, $layers);

OldSearch{
$oldeng=$eng; $oldsearch=$search; 
}

SetSearch{
getpref(BOOL,$sl)==false ? &Unlocked:&Locked; &Sync;
}
Unlocked{
setpref(STRING,$sen,$search); setpref(STRING,$se,$eng); &Search;
}
Locked{
&Search; $search=$oldsearch; $eng=$oldeng;
}
Search{
$tmp=getclipboard(); setclipboard(); id(ID_EDIT_COPY); $word=getclipboard();
$word=="" ? &URLBarSearch:$word==$URL ? &PromptSearch:&OpenSearch; setclipboard($tmp);
}
URLBarSearch{
id(ID_SELECT_URL); id(ID_EDIT_COPY); $word=getclipboard();
$word=="" ? $word=$URL:""; $word==$URL ? &PromptSearch:&OpenSearch;
}
PromptSearch{
$search=="" ? $search="Google":0; 
$search=="URL" ? $tip="Enter URL:":$tip="Search ".$search." for:";
setclipboard($tmp); $word=prompt($tip,$search); $word==""?"": &OpenSearch;
}
OpenSearch{
$how=getpref(INT, $so); $search!="Google"?"":&Umlauts;
$how==0 ? open($eng.$word):$how==1 ? &SearchNew:$how==2 ? &SearchBack:0; 
}
Umlauts{
$word=gsub("ä","%C3%A4",$word); $word=gsub("ö","%C3%B6",$word);
$word=gsub("ü","%C3%BC",$word); $word=gsub("ß","%C3%9F",$word);
$word=gsub("Ä","%C3%84",$word); $word=gsub("Ö","%C3%96",$word);
$word=gsub("Ü","%C3%9C",$word);
}
SearchNew{
&ResetURL; $layer==true ? pluginmsg(layers,"OpenURL",$eng.$word):opennew($eng.$word);
}
ResetURL{
setclipboard($URL); id(ID_SELECT_URL); id(ID_EDIT_PASTE); id(ID_SELECT_URL);
}
SearchBack{
&ResetURL; $layer==true ? pluginmsg(layers,"OpenURLBg",$eng.$word):OpenBg($eng.$word);
}

SN0{
setpref(INT,$so,0); &Sync;
}
SN1{
setpref(INT,$so,1); &Sync; 
}
SN2{
setpref(INT,$so,2); &Sync;
}

SearchLock{
$search==""? $search="Google":0; togglepref(BOOL,$sl); 
getpref(BOOL,$sl)==true ? $how="LOCKED on: ".$search:$how="UNLOCKED";
alert("The default Search Engine is now ".$how."\n\n\nTo set default engine:\n\n1. Unlock\n2. Click desired engine from menu\n3. Lock","Search Button",INFO);
&Sync;
}

AddEng{
$theurl="http://kmeleon.sourceforge.net/wiki/index.php?id=Search+Engines";
$layer=="true"? pluginmsg(layers,"OpenURL",$theurl):opennew($theurl);
$loaded="MacMen";
}
MacMen{
&Macros; &Menus; $loaded="";
}

JSEnable {
getpref(BOOL,$jscript)==false ? &JS:macros($macro);
}
JS{
&JSToggle; macros($macro); &JSToggle;
}

Highlight{
$macro="Find"; &JSEnable; 
}
Find{
open("javascript:(function(){var count=0, text, dv;text=prompt(%22Find and Highlight the following:%22, %22%22);if(text==null || text.length==0)return;dv=document.defaultView;function searchWithinNode(node, te, len){var pos, skip, spannode, middlebit, endbit, middleclone;skip=0;if( node.nodeType==3 ){pos=node.data.toUpperCase().indexOf(te);if(pos>=0){spannode=document.createElement(%22SPAN%22);spannode.style.backgroundColor=%22yellow%22;middlebit=node.splitText(pos);endbit=middlebit.splitText(len);middleclone=middlebit.cloneNode(true);spannode.appendChild(middleclone);middlebit.parentNode.replaceChild(spannode,middlebit);++count;skip=1;}}else if( node.nodeType==1&& node.childNodes&& dv.getComputedStyle(node, null).getPropertyValue(%22display%22)!=%22none%22){for (var child=0; child < node.childNodes.length; ++child){child=child+searchWithinNode(node.childNodes[child], te, len);}}return skip;}window.status=%22Searching for '%22+text+%22'...%22;searchWithinNode(document.body, text.toUpperCase(), text.length);window.status=%22Found %22+count+%22 occurrence%22+(count==1?%22%22:%22s%22)+%22 of '%22+text+%22'.%22;})();");
}

Keyword{
togglepref(BOOL,"keyword.enabled"); &Sync;
}

CheckKey{
$eng=getpref(STRING,"keyword.URL");
}
KeyEngBBN{
$eng="http://www.google.com/search?gfns=1&q=";
$string="BBN"; &KeyEng;
}
KeyEngIFL{
$eng="http://www.google.com/search?btnI=1&q=";
$string="IFL"; &KeyEng; 
}
KeyEngGoog{
$eng="http://www.google.com/search?q=";
$string="GOOG"; &KeyEng; 
}
KeyConfig{
&CheckKey; $eng=prompt("Enter Keyword Autosearch Engine:","Keyword Autosearch",$eng)
$eng==""?"":$string=""; $eng==""?"": &KeyEng;
}
KeyEng{
setpref(STRING,"keyword.URL",$eng); setpref(STRING,"keyword.eng",$string); &Sync;
alert("Keyword Autosearch Engine =  '".$eng."'\n\n\nKeyword engine will be updated when K-Meleon is restarted.","Keyword Autosearch Engine",INFO);
}

$taf="accessibility.typeaheadfind";

TAF{
togglepref(BOOL,$taf); &Sync;
}
TAFAuto{
togglepref(BOOL,$taf.".autostart"); 
$var=getpref(BOOL,$taf.".autostart")==true ? $bool=" - ENABLED. When a page loads, just typing will automatically start Find As You Type.":$bool = " - DISABLED ";
$tip="Find As You Type Auto Start"; &Sync; &AlertTAF; 
}
TAFLinks{
togglepref(BOOL,$taf.".linksonly"); 
$var=getpref(BOOL,$taf.".linksonly")==true ? $bool=" - ENABLED. Only links will be searched for as you start typing. You can override this by pressing  /  for all text, or  '  for links only.":$bool=" - DISABLED ";
$tip="Find As You Type Links Only"; &Sync; &AlertTAF; 
}
AlertTAF{
alert($tip.$bool,$tip,INFO);
}


# ----IMAGE & ZOOM MACROS----

ImageInfo{
$macro="ImgInfo"; &JSEnable;
}
ImgInfo {
$FrameURL=""; $PageURL=$FrameURL;
open("javascript: function chkframes(elem) { var rep; if (unescape(elem.location) == '".$PageURL."' || '".$PageURL."'=='') { rep=chkimg(elem); if (rep) return rep; } for (var f=0; f<elem.frames.length; f++) { rep=chkframes(elem.frames[f]); if (rep) return rep; } return; } function chkimg(elem){ if(elem.document.images.length>0){ var i; for(i=0;i<elem.document.images.length;i++){ if(unescape(elem.document.images[ i ].src) == '".$ImageURL."'){ return elem.document.images[i]; } } } } function XyZ() { var img=chkframes(window); if (img) { alert(\"* I M A G E   P R O P E R T I E S *\\r\\n\\r\\nURL:\\t\" + unescape(img.src) + \"\\r\\nWidth:\\t\" + img.width + \"\\r\\nHeight:\\t\" + img.height + \"\\r\\nALT:\\t\" + img.alt  + \"\\r\\nTitle:\\t\" + img.title); } } void(XyZ()); ");
}

ImageCache{
$i=index($ImageURL,"#");
$addr=$i==-1 ? $ImageURL:substr($ImageURL,0,$i);
$client=index($addr,"https://")==0 ? "HTTP-memory-only":"HTTP";
$var="about:cache-entry?client=".$client."&sb=1&key=".$addr;
index($addr,"http")==0 ? opennew($var):"";
}

ZoomInImage{
$macro="ZoomInImg"; &JSEnable;
}
ZoomInImg{
$FrameURL=""; $PageURL=$FrameURL;
open("javascript:function chkframes(elem) { var rep; if (unescape(elem.location) == '".$PageURL."' || '".$PageURL."'=='') { rep=chkimg(elem); if (rep) return rep; } for (var f=0; f<elem.frames.length; f++) { rep=chkframes(elem.frames[f]); if (rep) return rep; } return; } function chkimg(elem){ if(elem.document.images.length>0){ var i; for(i=0;i<elem.document.images.length;i++){ if(unescape(elem.document.images[ i ].src) == '".$ImageURL."'){ return elem.document.images[i]; } } } } function zoomImage(image, amt) { if(image.initialHeight == null) { /* avoid losing height information due to integer rounding while zooming out */ image.initialHeight = image.height; image.initialWidth = image.width; image.scalingFactor = 1; } image.scalingFactor *= amt; image.width = image.scalingFactor * image.initialWidth; image.height = image.scalingFactor * image.initialHeight; } function findIMG() { var img=chkframes(window); zoomImage(img,2); } void(findIMG());");
}

ZoomOutImage{
$macro="ZoomOutImg"; &JSEnable;
}
ZoomOutImg {
$FrameURL=""; $PageURL=$FrameURL;
open("javascript:function chkframes(elem) { var rep; if (unescape(elem.location) == '".$PageURL."' || '".$PageURL."'=='') { rep=chkimg(elem); if (rep) return rep; } for (var f=0; f<elem.frames.length; f++) { rep=chkframes(elem.frames[f]); if (rep) return rep; } return; } function chkimg(elem){ if(elem.document.images.length>0){ var i; for(i=0;i<elem.document.images.length;i++){ if(unescape(elem.document.images[ i ].src) == '".$ImageURL."'){ return elem.document.images[i]; } } } } function zoomImage(image, amt) { if(image.initialHeight == null) { /* avoid losing height information due to integer rounding while zooming out */ image.initialHeight = image.height; image.initialWidth = image.width; image.scalingFactor = 1; } image.scalingFactor *= amt; image.width = image.scalingFactor * image.initialWidth; image.height = image.scalingFactor * image.initialHeight; } function findIMG() { var img=chkframes(window); zoomImage(img,.5); } void(findIMG());");
}

ZoomInImages{
$macro="ZoomInImgs"; &JSEnable;
}
ZoomInImgs{
open("javascript:(function(){ function zoomImage(image, amt) { if(image.initialHeight == null) { /* avoid losing height information due to integer rounding while zooming out */ image.initialHeight = image.height; image.initialWidth = image.width; image.scalingFactor = 1; } image.scalingFactor *= amt; image.width = image.scalingFactor * image.initialWidth; image.height = image.scalingFactor * image.initialHeight; } for (i=0; i<document.images.length; ++i) zoomImage(document.images[i], 1.1); })();");
}

ZoomOutImages{
$macro="ZoomOutImgs"; &JSEnable;
}
ZoomOutImgs{
open("javascript:(function(){ function zoomImage(image, amt) { if(image.initialHeight == null) { /* avoid losing height information due to integer rounding while zooming out */ image.initialHeight = image.height; image.initialWidth = image.width; image.scalingFactor = 1; } image.scalingFactor *= amt; image.width = image.scalingFactor * image.initialWidth; image.height = image.scalingFactor * image.initialHeight; } for (i=0; i<document.images.length; ++i) zoomImage(document.images[i], .9092); })();");
}

ZoomImageLinks{
$macro="ZoomImgLinks"; &JSEnable;
}
ZoomImgLinks{
open("javascript:{var hrefs=document.getElementsByTagName('A'),images='';for(idx=0;idx<hrefs.length;idx++){href=hrefs[idx].href.toUpperCase(); if(href.indexOf('.GIF')>-1||href.indexOf('.BMP')>-1||href.indexOf('.JPEG')>-1||href.indexOf('.JPG')>-1||href.indexOf('.PNG')>-1)images+='<img src='+hrefs[idx]+'><br><br>';}images='<html><body><center><h2>Images</h2><br><br>'+images+'</center></body></html>';document.write(images);document.close();}");
}

ZoomInPage{
$macro="ZoomInPg"; &JSEnable;
}
ZoomInPg{
open("javascript:function zoomImage(image, amt) { if(image.initialHeight == null) {image.initialHeight = image.height; image.initialWidth = image.width; image.scalingFactor = 1; } image.scalingFactor *= amt; image.width = image.scalingFactor * image.initialWidth; image.height = image.scalingFactor * image.initialHeight; }function rZoomFont(n, node) {for ( var i = 0; i < node.childNodes.length; i++ ) {if (node.childNodes[i].nodeType == 1) rZoomFont(n, node.childNodes[i]);}startSize = getComputedStyle(node,'').getPropertyValue('font-size');startSize = Number(startSize.substr(0,startSize.length-2));lh = getComputedStyle(node,'').getPropertyValue('line-height');if (lh != 'normal') {lh = Number(lh.substr(0,lh.length-2)) * n + 'px';node.style.lineHeight = lh;}newSize = (startSize * n) + 'px';node.style.fontSize = newSize;}rZoomFont(1.1,document.getElementsByTagName('body')[0]); for (i=0; i<document.images.length; ++i) zoomImage(document.images[i], 1.1);");
}

ZoomOutPage{
$macro="ZoomOutPg"; &JSEnable;
}
ZoomOutPg{
open("javascript:function zoomImage(image, amt) { if(image.initialHeight == null) {image.initialHeight = image.height; image.initialWidth = image.width; image.scalingFactor = 1; } image.scalingFactor *= amt; image.width = image.scalingFactor * image.initialWidth; image.height = image.scalingFactor * image.initialHeight; }function rZoomFont(n, node) {for ( var i = 0; i < node.childNodes.length; i++ ) {if (node.childNodes[i].nodeType == 1) rZoomFont(n, node.childNodes[i]);}startSize = getComputedStyle(node,'').getPropertyValue('font-size');startSize = Number(startSize.substr(0,startSize.length-2));lh = getComputedStyle(node,'').getPropertyValue('line-height');if (lh != 'normal') {lh = Number(lh.substr(0,lh.length-2)) * n + 'px';node.style.lineHeight = lh;}newSize = (startSize * n) + 'px';node.style.fontSize = newSize;}rZoomFont(0.9092,document.getElementsByTagName('body')[0]); for (i=0; i<document.images.length; ++i) zoomImage(document.images[i], .9092);");
}

$zoom="kmeleon.general.zoomButtons";

ZoomSet{
setpref(STRING,$zoom,$var); &Sync;
alert("Zoom In/Out toolbar buttons will effect ".$what,Zoom Toolbar Buttons,INFO);
}
ZoomText{
$var="text"; $what="text only."; &ZoomSet;
}
ZoomImages{
$var="images"; $what="images only."; &ZoomSet;
}
ZoomPage{
$var=""; $what="both images and text."; &ZoomSet;
}

ZoomIn{
$zi=getpref(STRING,$zoom); $zi==""? &ZoomInPage:&ZoomInCheck;
}
ZoomInCheck{
$zi=="text"? id(ID_FONT_INCREASE):&ZoomInImages;
}

ZoomOut{
$zo=getpref(STRING,$zoom); $zo==""? &ZoomOutPage:&ZoomOutCheck;
}
ZoomOutCheck{
$zo=="text"? id(ID_FONT_DECREASE):&ZoomOutImages;
}


# ----BOOKMARK MACROS---- 

FileBook{
plugin(bookmarks,Add); plugin(bookmarks,Edit);
}
FileHot{
plugin(hotlist,Add); plugin(hotlist,Edit);
}
FileFav{
plugin(favorites,Add); plugin(favorites,Edit);
}

FavToolbar{
$tf="kmeleon.plugins.favorites.toolbarFolder";
$folder=getpref(STRING,$tf); $folder==""? $folder="Links":""; 
$folder=prompt("Enter the name of the folder to be displayed on your favorites toolbar.","Favorites Toolbar Folder",$folder);
$folder==""?"":setpref(STRING,$tf,$folder);
$folder==""?"":alert("Favorites Toolbar Folder will be  ' " .$folder." '  when K-Meleon is restarted","Favorites Toolbar Folder",INFO);
}
ChangeLinks{
$tit="kmeleon.plugins.favorites.title";
$favtit=getpref(STRING, $tit); $favtit==""? $favtit="Links":""; 
$favtit=prompt("Enter the title to be displayed on your favorites toolbar.", "Toolbar Title     (Leave blank or click Cancel to remove title)", $favtit);
setpref(STRING,$tit,$favtit);
alert("Title change will be apparent when K-Meleon is restarted","Favorites Toolbar Title",INFO);
}

Chevron{
$chevron="kmeleon.plugins.".$plugin.".chevron"; togglepref(BOOL,$chevron);
&Sync; getpref(BOOL,$chevron)==true ? $var="- ENABLED":$var="- DISABLED";
alert("\nToolbar Chevron ".$var."\n\n\nChange will be visible when K-Meleon is restarted.","Toolbar Chevron",INFO);
}
BookChev{
$plugin="bookmarks"; &Chevron;
}
FavChev{
$plugin="favorites"; &Chevron;
}
HotChev{
$plugin="hotlist"; &Chevron;
}

$ol="ID_OPEN_LINK"; $nw="ID_OPEN_LINK_IN_NEW_WINDOW"; 
$bw="ID_OPEN_LINK_IN_BACKGROUND"; $lo="layers(OpenURL)"; $bl="layers(OpenURLBg)";
$book="kmeleon.plugins.bookmarks.openurl";
$fav="kmeleon.plugins.favorites.openurl";
$hot="kmeleon.plugins.hotlist.openurl";

openurl{
setpref(STRING,"kmeleon.plugins.".$plugin.".openurl",$how); &Sync;
}

OpenBook1{
$plugin="bookmarks"; $how=$ol; &openurl;
}
OpenBook2{
$plugin="bookmarks"; $how=$nw; &openurl;
}
OpenBook3{
$plugin="bookmarks"; $how=$lo; &openurl;
}
OpenBook4{
$plugin="bookmarks"; $how=$bl; &openurl;
}
OpenBook5{
$plugin="bookmarks"; $how=$bw; &openurl;
}

OpenFav1{
$plugin="favorites"; $how=$ol; &openurl;
}
OpenFav2{
$plugin="favorites"; $how=$nw; &openurl;
}
OpenFav3{
$plugin="favorites"; $how=$lo; &openurl;
}
OpenFav4{
$plugin="favorites"; $how=$bl; &openurl;
}
OpenFav5{
$plugin="favorites"; $how=$bw; &openurl;
}

OpenHot1{
$plugin="hotlist"; $how=$ol; &openurl;
}
OpenHot2{
$plugin="hotlist"; $how=$nw; &openurl;
}
OpenHot3{
$plugin="hotlist"; $how=$lo; &openurl;
}
OpenHot4{
$plugin="hotlist"; $how=$bl; &openurl;
}
OpenHot5{
$plugin="hotlist"; $how=$bw; &openurl;
}


# ----GROUP MACROS----

$kg="kmeleon.group."; $URL="";
$rl="ReplaceLayersInWindow";
$al="AddLayersToWindow";
$gl="GetLayersInWindow";
$fn="FindNick"; $lc="";
$gn="kmeleon.groupnames";

ReadNames{
$names=getpref(STRING, $gn);
}
Checkname{
$checkname="\n".$name."\n"; $exists=index($names,$checkname);
}

SaveAsGroup{
&ReadNames; $names==""? $names="\n":0;
$group=pluginmsgex(layers,$gl,"",STRING);
$name=prompt("Enter a Group Name to save these layers as:","Group Name?","");
$name==""?"": &Checkname; 
$name==""?"": $exists!=-1? &ConfirmGroup:&SaveGroup;
}
ConfirmGroup{
confirm("Group Name exists. Overwrite?","Group Name  '" . $name . "'  Found",YESNO,EXCLAIM)=="YES"? setpref(STRING, $kg.$name,$group):&SaveAsGroup;
}
SaveGroup{  
setpref(STRING,$gn,$names.$name."\n"); setpref(STRING,$kg.$name,$group);
confirm("\nType  '" . $name . "'  in the URL Bar and press SHIFT+ENTER to open it only or ALT+ENTER to open it next to the current layers.\n\n\nOpen this Group everytime you start K-Meleon?","Group Saved As:  ".$name,YESNO)=="NO" ?"":&SetGroup;
}

SavedGroups{
&ReadNames; $names==""? $var="Folder": $var="Group";
$names==""? $names="No Group Names Found" ."\n":0;
$open=confirm($names."\n\nOpen a ".$var." now?", "Saved Groups")=="OK"? &OpenGroup:"";
}

URLToGroup{
$name=prompt("Enter a Group Name to add this URL to:","Group Name?","");
getpref(STRING,$kg.$name); $group=getpref(STRING,$kg.$name);
$name==""?"":$group==""? &NoGroup:&AddURL;
}
AddURL{
setpref(STRING,$kg.$name,$group.$URL."\t");
alert("The URL for  '".$TITLE."'  has been added to Group: ".$name,"Group:  '" . $name . "'  Updated",INFO);
}

NoGroup{
alert("No Group Found");
}

LayersToGroup {
$name=prompt("Enter a Group Name to add these layers to","Group Name?","");
getpref(STRING,$kg.$name); $group=getpref(STRING,$kg.$name);
$name==""?"": $group==""? &NoGroup:&AddLayers;
}
AddLayers{
$curr=pluginmsgex(layers,"GetLayersInWindow","",STRING);
setpref(STRING,$kg.$name,$group.$curr);
alert("Layers added to Group: ".$name,"Group:  '".$name."'  Updated",INFO);
}

DeleteGroup{
&ReadNames; $names==""? &NoGroup:&ConfirmDelete;
}
ConfirmDelete{
$open=confirm($names."\n\nDo you want to delete one of these groups?","Saved Groups",YESNO);
$open=="NO"? "":&PromptDelete;
}
PromptDelete{
$name=prompt("Enter Name of Group to Delete:","Group Name?","");
$name==""?"": &Checkname;
$name==""?"": $exists!=-1? &DeleteURLs:&NoGroup;
}
DeleteURLs{
$names=sub($checkname,"\n",$names);
setpref(STRING,$gn,$names); delpref($kg.$name); 
alert("Group: '".$name."' deleted.","Group Deleted",INFO);
}

PluginCheck{
getpref(BOOL,"kmeleon.plugins.bookmarks.load")==false ? $hot=getpref(BOOL,"kmeleon.plugins.hotlist.load"):"";
$hot==false ? $tip="Enter Group Name":$tip="Enter Group Name or Folder Nick";
}

SetOnly{
setpref(STRING,$og,"layers(ReplaceLayersInWindow)");
}
SetNext{
setpref(STRING,$og,"layers(AddLayersToWindow)");
}
GroupOnly{
$lc=$rl; &SetOnly; &OpenGroup; $lc=$al; &SetNext; 
}
OpenGroup{
$old=getclipboard(); setclipboard(); id(ID_SELECT_URL); id(ID_EDIT_COPY);
$name=getclipboard(); $name==""?$name=$URL:"";
$name==$URL? &PromptGroup:&ChkGroup; setclipboard($old);
}
PromptGroup{ 
&PluginCheck; $name=prompt($tip,"Group Name?","");
$name==""?"":&ChkGroup;
}
ChkGroup{
$group=getpref(STRING,$kg.$name); $group==""? &ChkFolder:&Group;
}
ChkFolder{
$plugin="bookmarks"; $group=pluginmsgex($plugin,$fn,$name,STRING)==""? $plugin="hotlist":"";
pluginmsgex($plugin,$fn,$name,STRING)==""? &ResetURL:&Folder;
}
Group{
$lc==""? $lc=$al:""; pluginmsg(layers,$lc,$group);
}
Folder{
setclipboard($name); id(ID_SELECT_URL); id(ID_EDIT_PASTE); id(ID_NAV_GO); &ResetURL;
}

# STARTUP GROUP & LAST SESSION MACROS

$loaded=""; 
$sg="kmeleon.general.startGroup";
$sn="kmeleon.general.startGroupName";
$sh="kmeleon.general.startHome";
$og="kmeleon.general.opengroup";
$gt="kmeleon.grouptemp";
$kl="kmeleon.grouplast";
$ln="ID_OPEN_LINK_IN_NEW_WINDOW|ID_OPEN_LINK_IN_BACKGROUND";

StartGroup{
$oldname=getpref(STRING,$sn); &PluginCheck;
$name=prompt($tip,"Startup Group Name?",$oldname);
$name==""?"":$group=getpref(STRING,$kg.$name)==""? &FolderCheck:&SetGroup;
}
FolderCheck{
$plugin="bookmarks"; $group=pluginmsgex($plugin,$fn,$name,STRING)==""? $plugin="hotlist":0;
$group=pluginmsgex($plugin,$fn,$name,STRING)==""? &NoStart:&SetGroup;
}
SetGroup{
setpref(INT,$sg,2); setpref(STRING,$sn,$name); setpref(BOOL,$sh,false); &Sync;
}
NoStart{
&NoGroup; setpref(STRING,$sn,$oldname); &StartGroup;
}
StartLast{
setpref(INT,$sg,3); setpref(BOOL, $sh, false); &Sync;
}
StartHome{
setpref(INT,$sg,0); setpref(BOOL,$sh,true); &Sync;
}
StartBlank{
setpref(INT,$sg,1); setpref(BOOL,$sh,false); &Sync;
}

GroupStart{
$oldclip=getclipboard(); $name=getpref(STRING,$sn); 
id(ID_SELECT_URL); id(ID_EDIT_COPY); $theurl=getclipboard();
$theurl=="about:blank"? &LayersCheck:"";
setclipboard($oldclip); $loaded="";
}
LayersCheck{ 
$layer=="true"? &StartLayers:&StartWindows;
}
StartLayers{
$lc=$rl; &SetOnly; getpref(INT,$sg)==3 ? &OpenLast:&OpenStart;
$lc=$al; &SetNext;
}
OpenLast{
$group=getpref(STRING,$kl); $group==""?"":&Group;
}
OpenStart{
$name=getpref(STRING,$sn); &ChkGroup;
}
StartWindows{
setpref(STRING,$og,"ID_OPEN_LINK|ID_OPEN_LINK_IN_BACKGROUND"); 
$name=getpref(STRING,$sn); getpref(INT,$sg)==3 ?"":&ChkFolder;
setpref(STRING,$og,$ln);
}

GetSetTemp{
$group=pluginmsgex(layers,$gl,"",STRING); setpref(STRING,$gt,$group);
}
SetLast{
$group=getpref(STRING, $gt); setpref(STRING,$kl,$group); 
}
OpenClosed{
$lc=$rl; $group=getpref(STRING,$gt); $curr=pluginmsgex(layers,$gl,"",STRING);
$group==$curr?"":$curr=""; $curr==""? &Group:"";
}

ReloadAll{
pluginmsg(layers,"AllLayersInWindow","ID_NAV_RELOAD");
}

Home{
getpref(INT,$sg)!=2 ? id(ID_NAV_HOME):&OpenStart;
}


# EVENT MACROS w/LAST SESSION & STARTUP GROUP SUPPORT

OnStartup{
&SetLast;
$layer=="true"? &SetNext:setpref(STRING,$og,$ln);
getpref(INT,$sg)<2 ? "":$loaded="GroupStart"; &Sync;
}

OnLoad{
$loaded==""?"":macros($loaded); &GetSetTemp; &Sync;
}

OnCloseGroup{
&GetSetTemp;
}

OnWMAppExit{
&GetSetTemp;
}

OnQuit{
&SetLast; getpref(BOOL,$uar)!=true? &UA:""; 
}


# ----MAIL MACROS----

ReadClient{
$mclient=getpref(STRING,"kmeleon.general.MailClient");
$nclient=getpref(STRING,"kmeleon.general.NewsClient");
}
ParseMail{
&ReadClient; $which="Mail"; $client=$mclient; $ex="hotmail.com";
}
ParseNews{
&ReadClient; $which="News"; $client=$nclient; $ex="groups.google.com";
}
Mail{
&ParseMail;
$mclient==""? &ClientPath:$web=index($mclient,"http")==-1?exec($mclient):opennew($mclient);
}
News{
&ParseNews;
$nclient==""? &ClientPath:$web=index($nclient,"http")==-1?exec($nclient):opennew($nclient);
}
ClientPath{
&ReadClient; $client==""?$client="C:\\Program Files\\Outlook Express\\msimn.exe":0;
$client=prompt("Enter path or full URL (e.g. http://".$ex. ") of your client.",$which." Client Path",$client);
$client==""?"":setpref(STRING,"kmeleon.general.".$which."Client",$client);
$client==""?"":alert("\n".$which." Client =  '".$client."'\n\n\nClick on the ".$which." button to go your ".$which.".",$which." Client Set",INFO);
}
MCFG{
&ParseMail; &ClientPath;
}
NCFG{
&ParseNews; &ClientPath;
}

Compose{
$old=getclipboard(); open("mailto:"); &ResetURL; setclipboard($old);
}

EmailURL{
$old=getclipboard(); open("mailto:?subject=".gsub("&","%26",$TITLE)."&body=".gsub("&","%26",$URL));
&ResetURL; setclipboard($old);
}
EmailImageURL{
$old=getclipboard(); open("mailto:?subject=".gsub("&","%26",$TITLE)."&body=".gsub("&","%26",$IMAGEURL));
&ResetURL; setclipboard($old);
}
EmailFrameURL{
$old=getclipboard(); open("mailto:?subject=".gsub("&","%26",$TITLE)."&body=".gsub("&","%26",$FRAMEURL));
&ResetURL; setclipboard($old);
}
EmailText{
$macro="EmailTxt"; &JSEnable;
}
EmailTxt{
open("javascript:(function(){var sel=document.getSelection();if(sel)self.location=\"mailto:?subject=\"+document.title+\"&body=\"+escape(sel)})()");
}


# ----PRIVACY MACROS---- 

$animation="image.animation_mode";
$colors="browser.display.use_document_colors";
$cookies="network.cookie.cookieBehavior";
$images="network.image.imageBehavior";
$java="security.enable_java";
$jscript="javascript.enabled";
$popups="dom.disable_open_during_load";
$referer="network.http.sendRefererHeader";

AniToggle {
togglepref(STRING,$animation,"normal","none"); &Sync;
getpref(STRING,$animation)=="none"?$tip="BLOCKED":$tip="ALLOWED";
statusbar("Animation ".$tip);
}

CToggle{
togglepref(BOOL,$colors); &Sync;
getpref(BOOL,$colors)==false ? $tip="BLOCKED":$tip="ALLOWED";
statusbar("Page Colors ".$tip);
}

CKToggle {
togglepref(INT,$cookies,2,0); &Sync;
getpref(INT,$cookies)==2 ? $tip="BLOCKED":$tip="ALLOWED";
statusbar("Cookies ".$tip);
}

PBToggle{
togglepref(BOOL,$popups); &Sync;
getpref(BOOL,$popups)==true ? $tip="BLOCKED":$tip="ALLOWED";
statusbar("Popups ".$tip);
}

JToggle{
togglepref(BOOL,$java); &Sync;
getpref(BOOL,$java)==false ? $tip="BLOCKED":$tip="ALLOWED";
statusbar("Java ".$tip);
}

JSToggle{
togglepref(BOOL,$jscript); &Sync; 
getpref(BOOL,$jscript)==false ? $tip="BLOCKED":$tip="ALLOWED";
statusbar("JavaScript ".$tip);
}

ImgToggle {
togglepref(INT,$images,2,0); &Sync;
getpref(INT,$images)==2 ? $tip="BLOCKED":$tip="ALLOWED";
statusbar("Images ".$tip);
}

RefToggle {
togglepref(INT,$referer,2,0); &Sync;
getpref(INT,$referer)==2 ? $tip="ALLOWED":$tip="BLOCKED";
statusbar("Referer ".$tip);
}

$ua="general.useragent.override"; $uas="";
$pu="kmeleon.privacy.useragent"; $ps=".string";
$uar="kmeleon.privacyUA.noReset";

UA{
delpref($ua); &Sync;
}
UA1{
$uas=getpref(STRING,$pu."1".$ps); &SetUA;
}
UA2{
$uas=getpref(STRING,$pu."2".$ps); &SetUA;
}
UA3{
$uas=getpref(STRING,$pu."3".$ps); &SetUA;
}
UACust{
$uas=getpref(STRING,$ua);
$uas=prompt("Enter the User Agent string to identify K-Meleon as:","Custom User Agent",$uas);
$uas==""?"":&SetUA;
}
SetUA{
setpref(STRING,$ua,$uas); &Sync;
}
ResetUA{
togglepref(BOOL,$uar); &Sync;
}

ClearAll{
plugin(privacy,ClearCache); plugin(privacy,ClearCookies); 
plugin(privacy,ClearHistory); plugin(privacy,ClearMRU);
plugin(privacy,ClearSignon);
}

KillFlash{
$old=getclipboard(); $macro="KillFlsh"; &JSEnable; &ResetURL; setclipboard($old);
}
KillFlsh{
open(" javascript: (function(){var d=document; function K(N,t) { var b = d.createElement('div'); b.style.width=N.width; b.style.height=N.height; b.innerHTML='<del>' + t + '</del>'; N.parentNode.replaceChild(b,N); } function Z(t) { var T = d.getElementsByTagName(t), i; for (i=T.length-1;i+1;--i) K(T[ i ],t); } Z('object'); Z('embed');})(); (function(){document.close();})(); ");
}

KillApplets{
$old=getclipboard(); $macro="KillApp"; &JSEnable; &ResetURL; setclipboard($old);
}
KillApp{
open(" javascript: (function(){var d=document; function K(N,t) { var b = d.createElement('div'); b.style.width=N.width; b.style.height=N.height; b.innerHTML='<del>' + t + '</del>'; N.parentNode.replaceChild(b,N); } function Z(t) { var T = d.getElementsByTagName(t), i; for (i=T.length-1;i+1;--i) K(T[ i ],t); } Z('applet');})(); (function(){document.close();})(); ");
}


# ----LAYER MACROS----

$catch="kmeleon.plugins.layers.catchOpen";
$mode="kmeleon.general.mode";
$flip="dom.disable_window_flip";

LayersOnly{
$var="LAYERS ONLY"; setpref(BOOL,$layers,true); setpref(BOOL,$catch,true);
$bool=true; $menu=false; &Scripts; setpref(STRING,$mode,$var); &Sync; 
alert("All new windows will open in layers. If switching from Windows Only mode, you'll need to restart K-Meleon for Layers  Only mode to take effect.","Browsing Mode:  ".$var,INFO);
}

LayersWindows{
$var=""; setpref(BOOL,$layers,true);
setpref(BOOL,$catch,false); setpref(STRING,$mode,$var); &Sync; 
alert("You will now be able to browse using layers and multiple windows. If switching from Windows Only mode, you'll need to  restart K-Meleon for Layers and Windows mode to take effect.","Browsing Mode:  Layers & Windows", INFO);
}

WindowsOnly{
$var="WINDOWS ONLY"; $quest=confirm("\nWindows Only mode will disable the layers plugin. Groups and Sessions will not be  available, only Folder Groups for Bookmarks & Hotlist plugin users.\n\n\nDo you want to disable the layers plugin?","Browsing Mode:  ".$var,YESNO,EXCLAIM);
$quest=="NO"?"":&SetWO; 
}
SetWO{
setpref(BOOL,$layers,false); setpref(BOOL,$catch,false); setpref(STRING,$mode,$var); &Sync; 
alert("Restart K-Meleon for Windows Only mode to take effect.","Browsing Mode:  ".$var,INFO);
}

BlockScripts{
$bool=true; $menu=false; &Scripts; &Sync; 
alert("Sites are now BLOCKED from hiding toolbars, resizing the browser and disabling context menus.",Scripts,INFO);
}
AllowScripts{
$bool=false; $menu=true; &Scripts; &Sync;
alert("Sites are now ALLOWED to hide toolbars, resize the browser and disable context menus.",Scripts, INFO);
}
Scripts{
setpref(BOOL,$flip,$bool); 
setpref(BOOL,"dom.disable_window_move_resize",$bool);
setpref(BOOL,"dom.disable_window_open_feature.*",$bool); 
setpref(BOOL,"dom.disable_window_open_feature.minimizable",$bool);
setpref(BOOL,"dom.disable_window_open_feature.titlebar",$bool); 
setpref(BOOL,"dom.disable_window_open_feature.close",$bool);
setpref(BOOL,"dom.disable_window_open_feature.toolbar",$bool); 
setpref(BOOL,"dom.disable_window_open_feature.location",$bool);
setpref(BOOL,"dom.disable_window_open_feature.directories",$bool); 
setpref(BOOL,"dom.disable_window_open_feature.personalbar",$bool);
setpref(BOOL,"dom.disable_window_open_feature.menubar",$bool);
setpref(BOOL,"dom.disable_window_open_feature.scrollbars",$bool);
setpref(BOOL,"dom.disable_window_open_feature.resizable",$bool);
setpref(BOOL,"dom.disable_window_open_feature.status",$bool);
setpref(BOOL,"dom.disable_window_status_change",$bool);
setpref(BOOL,"dom.event.contextmenu.enabled",$menu);
}

$num="kmeleon.plugins.layers.numbers";
$style="kmeleon.plugins.layers.style";

LayerAlert{
alert("Some changes will only be fully apparent when the next layer is opened.","Layer Appearance Change Completed",INFO);
}
Enumerate{
togglepref(BOOL,$num); &Sync; &LayerAlert;
}
Style{
setpref(INT,$style,$int); &Sync; &LayerAlert;
}
Pressed{
$int=2; &Style;
}
Pressed+{
$int=10; &Style;
}
Buttons{
$int=6; &Style;
}
Buttons+{
$int=14; &Style;
}

LayersTitle{
$lt="kmeleon.plugins.layers.title";
$var=getpref(STRING, $lt); $var==""?$var="Layers:":"";
$var=prompt("Enter layer bar title:","Layer Bar Title     ( To remove title, leave blank or Cancel )",$var);
setpref(STRING,$lt,$var);
alert("Layer bar title = ".$var."\n\nChange will be visible when K-Meleon is restarted.","Layer Bar Title",INFO);
}

LayerWidth{
$max="kmeleon.plugins.layers.maxWidth";
$min="kmeleon.plugins.layers.minWidth";
$maxwid=getpref(INT,$max); $minwid=getpref(INT,$min);
$maxwid==0 ? $maxwid=35:"";
$maxwid=prompt("Enter Maximum Width:",Maximum Width,$maxwid);
$maxwid==""?"":setpref(INT,$max,$maxwid);
$maxwid==""?"":$minwid=prompt("Enter Minimum Width:",Minimum Width,$minwid);
$maxwid==""?"":$minwid==""?"":setpref(INT,$min,$minwid);
$maxwid==""?"":$minwid==""?"":alert("Maximum = ".$maxwid."\nMinimum = ".$minwid,"Layer Bar Button Width",INFO);
}

$onClose="kmeleon.plugins.layers.onCloseOption";
$onOpen="kmeleon.plugins.layers.onOpenOption";

SetCloseRight{
setpref(INT,$onClose,2); &Sync;
}
SetCloseLeft{
setpref(INT,$onClose,1); &Sync;
}
SetCloseLast{
setpref(INT,$onClose,0); &Sync;
}
SetOpenNext{
setpref(INT,$onOpen,1); &Sync;
}
SetOpenLast{
setpref(INT,$onOpen,0); &Sync;
}

$confirm="kmeleon.plugins.layers.confirmClose";

ConfirmToggle{
togglepref(BOOL,$confirm); &Sync;
getpref(BOOL,$confirm)==false ? $tip="DISABLED":$tip="ENABLED";
statusbar("Confirm before close ".$tip);
}

Selected{
$old=getclipboard(); setclipboard(); id(ID_EDIT_COPY); $add=getclipboard();
}

New{
&Selected; setclipboard($URL); id(ID_SELECT_URL); id(ID_EDIT_PASTE); 
$add==""? &Open:(getpref(STRING,$ou)!="ID_OPEN_LINK"? &PasteSel:&OpenNew);
}
Open{
$layer==true ? plugin(layers,Open):id(ID_NEW_BROWSER); setclipboard($old);
}
OpenNew{
&ResetURL; setclipboard($add);
$layer==true ? pluginmsg(layers,OpenURL,"about:blank"):opennew("about:blank"); 
$loaded="go"; 
}

PasteSel{
setclipboard($add); $add==""?id(ID_NAV_GO): &go; setclipboard($old); 
}

go{
&PasteGo; setclipboard($old); $loaded="";
}


# ----URL BAR MACROS---- 

SelectGo{
&Selected; &PasteSel;
}

Copy{
id(ID_SELECT_URL); id(ID_EDIT_COPY);
}

PasteGo{
id(ID_SELECT_URL); id(ID_EDIT_PASTE); id(ID_NAV_GO);  
}

PasteSearch{
id(ID_SELECT_URL); id(ID_EDIT_PASTE); &Search;
}

ResetURLBar{
$old = getclipboard(); &ResetURL; setclipboard($old);
}

$ou="kmeleon.general.openurl";

URLBar{
setpref(STRING,$ou,$var); &Sync
}
URLInCurr{
$var="ID_OPEN_LINK"; &URLBar;
}
URLInLayer{
$var="layers(OpenURL)"; &URLBar;
}
URLInNew{
$var="ID_OPEN_LINK_IN_NEW_WINDOW"; &URLBar;
}
URLInBgLayer{
$var="layers(OpenURLBg)"; &URLBar;
}
URLInBg{
$var="ID_OPEN_LINK_IN_BACKGROUND"; &URLBar;
}

OpenInNew{
$old=getpref(STRING,$ou); $layer==true? &URLInLayer:&URLInNew;
id(ID_NAV_GO); setpref(STRING,$ou,$old);
}

up_directory {
$var=dirname($URL); $var=="."?"":open($var);
}

top_level {
$macro="top"; &JSEnable;
}
top{
open("javascript:location.pathname = ""; void 0");
}

$dom="kmeleon.general.domComplete"; 

ctrl_enter{
$var="0"; &GetComplete; &Domain;
}
ctrl_alt_enter{
$var="1"; &GetComplete; &Domain;
}
ctrl_shift_enter{
$var="2"; &GetComplete; &Domain;
}

GetComplete{
$prefix=getpref(STRING,$dom.$var.".prefix");  
$prefix==""? $prefix="http://www.":"";
$suffix=getpref(STRING,$dom.$var.".suffix");
$var=="0"?($suffix==""?$suffix=".com":"";):"";
$var=="1"?($suffix==""?$suffix=".net":"";):""; 
$var=="2"?($suffix==""?$suffix=".org":"";):"";
}

Domain{
$old=getclipboard(); setclipboard(); id(ID_SELECT_URL); id(ID_EDIT_COPY);
$theurl=getclipboard(); $theurl==""? &ResetURL: &Complete; setclipboard($old);
}
Complete{
$fullurl=$prefix.$theurl.$suffix;
setclipboard($fullurl); id(ID_EDIT_PASTE); id(ID_NAV_GO); 
}

CEConfig{
$var="0"; &GetComplete; $tip="Ctrl + Enter"; &DomConfig;
}
CAEConfig{
$var="1"; &GetComplete; $tip="Ctrl + Alt + Enter"; &DomConfig;
}
CSEConfig{
$var="2"; &GetComplete; $tip="Ctrl + Shift + Enter"; &DomConfig;
}

DomConfig{
$prefix=prompt("Prefix:",$tip,$prefix);
$prefix==""?"":setpref(STRING,$dom.$var.".prefix", $prefix);
$prefix==""?"":$suffix=prompt("Suffix:",$tip,$suffix);
$prefix==""?"":$suffix==""?"":setpref(STRING,$dom.$var.".suffix", $suffix);
$prefix==""?"":$suffix==""?"":alert($prefix."  +  ".$suffix,$tip,INFO);
}


# ----DOWNLOAD MACROS----

$sd="kmeleon.general.saveDir";
$sp="kmeleon.general.savePageDir";
$si="kmeleon.general.saveImageDir";
$sld="kmeleon.general.saveLinkDir";
$dir="kmeleon.general.profileDir";
$path=getpref(STRING, $dir);

GetDir{
$which=getpref(STRING,$el);
}
GetDef{
$el=$sd; $dl="Default"; &GetDir;
}
GetPage{
$el=$sp; $dl="Save Page As"; &GetDir;
}
GetImage{
$el=$si; $dl="Save Image As"; &GetDir;
}
GetLink{
$el=$sld; $dl="Save Link As"; &GetDir;
}

Save{
&GetDef; $oldsave=$which; macros($get); setpref(STRING,$sd,$which); 
id($id); setpref(STRING,$sd,$oldsave);
}

SavePage{
$get="GetPage"; $id="ID_FILE_SAVE_AS"; &Save; 
}

SaveFrame{
$get="GetPage"; $id="ID_FILE_SAVE_FRAME_AS"; &Save; 
}

SaveImage{
$get="GetImage"; $id="ID_SAVE_IMAGE_AS"; &Save; 
}

SaveLink{
$get="GetLink"; $id="ID_SAVE_LINK_AS"; &Save; 
}

SetDir{
$savetip="Enter Directory Path. (Include backslash at the end of folder name.)";
$which==""?$which=$path:"";
$which=prompt($savetip,$dl,$which);
$which==""?"":setpref(STRING,$el,$which);
$which==""?"":$savepath=$which; $which==""?"":&AlertSave;
}

DefaultDir{
&GetDef; &SetDir;
}

PageDir{
&GetPage; &SetDir;
}

ImageDir{
&GetImage; &SetDir;
}

LinkDir{
&GetLink; &SetDir;
}

AlertSave{
&GetDef; $def=$which; $dtip=$dl; &GetPage; $page=$which; $ptip=$dl;
&GetImage; $image=$which; $itip=$dl; &GetLink; $link=$which; $ltip=$dl;
alert($dtip."  =  ".$def.\n.$ptip."  =  ".$page.\n.$itip."  =  ".$image.\n.$ltip."  =  ".$link, "Download Directories",INFO); 
}


# ----MOUSE GESTURES----

$mouse="kmeleon.plugins.gestures.";

Gestures{
togglepref(BOOL,$mouse."load"); &Sync;
getpref(BOOL,$mouse."load")==true ? $tip="ENABLED":$tip="DISABLED"; 
alert("Mouse Gestures will be ".$tip." when K-Meleon is restarted.","Mouse Gestures ".$tip,INFO);
}

GestGuide{
$var=confirm("*M O U S E   G E S T U R E S   C O N F I G U R A T I O N   G U I D E*\n\nThis guide will present a prompt for each available mouse gesture. Each prompt will show the gesture in the title bar. Enter the command or macro that you'd like to use for each gesture. For your convenience, you can copy and paste the command or macro from the available commands list.\n\n\nDo you want to view a list of the available commands and macros to use for the gestures? (Recommended)","MOUSE GESTURES",YESNOCANCEL);
$var=="YES"? &Commands1st:0; $var=="NO"? &left:0; $var=="CANCEL"?"":0;
}
Commands1st{
&Commands; &left;
}

GestCFG{
$cmd=getpref(STRING,$mouse.$gest);
$cmd=prompt("Enter ID_COMMAND, PLUGIN COMMAND, or MACRO for this gesture.",$gest,$cmd);
$cmd==""?"":setpref(STRING,$mouse.$gest,$cmd); 
$cmd==""?"":confirm($gest."  =  ".$cmd."\n\nConfigure next gesture?","MOUSE GESTURE",YESNO)=="YES"? macros($next):0;
}

left{
$gest="left"; $next="right"; &GestCFG;
}
right{
$gest="right"; $next="down"; &GestCFG;
}
down{
$gest="down"; $next="downleft"; &GestCFG;
}
downleft{
$gest="downleft"; $next="downright"; &GestCFG;
}
downright{
$gest="downright"; $next="up"; &GestCFG;
}
up{
$gest="up"; $next="upleft"; &GestCFG;
}
upleft{
$gest="upleft"; $next="upright"; &GestCFG;
}
upright{
$gest="upright"; $next="left"; &GestCFG;
}


# ----BUTTON & MENU SYNC----

Sync{

&EngineSync;
setcheck("macros(SN0)",getpref(INT,$so)==0? true:false);
setcheck("macros(SN1)",getpref(INT,$so)==1? true:false);
setcheck("macros(SN2)",getpref(INT,$so)==2? true:false);
setcheck("macros(SearchLock)",getpref(BOOL,$sl));

setcheck("macros(Keyword)",getpref(BOOL,"keyword.enabled"));
setcheck("macros(KeyEngBBN)",getpref(STRING,"keyword.eng")=="BBN"? true:false);
setcheck("macros(KeyEngIFL)",getpref(STRING,"keyword.eng")=="IFL"? true:false);
setcheck("macros(KeyEngGoog)",getpref(STRING,"keyword.eng")=="GOOG"? true:false);
setcheck("macros(KeyConfig)",getpref(STRING,"keyword.eng")==""? true:false);

setcheck("macros(TAF)",getpref(BOOL,$taf)); 
setcheck("macros(TAFAuto)",getpref(BOOL,$taf.".autostart")); 
setcheck("macros(TAFLinks)", getpref(BOOL,$taf.".linksonly")); 

getpref(BOOL,$sh)==true ? setpref(INT,$sg,0):"";
setcheck("macros(StartHome)",getpref(BOOL,$sh));
setcheck("macros(StartBlank)",getpref(INT,$sg)==1? true:false);
setcheck("macros(StartGroup)",getpref(INT,$sg)==2? true:false);
setcheck("macros(StartLast)",getpref(INT,$sg)==3? true:false);

setcheck("macros(ZoomPage)",getpref(STRING,$zoom)==""? true:false);
setcheck("macros(ZoomText)",getpref(STRING,$zoom)=="text"? true:false);
setcheck("macros(ZoomImages)",getpref(STRING,$zoom)=="images"? true:false);

setcheck("macros(BookChev)",getpref(BOOL,"kmeleon.plugins.bookmarks.chevron"));
setcheck("macros(FavChev)",getpref(BOOL,"kmeleon.plugins.favorites.chevron"));
setcheck("macros(HotChev)",getpref(BOOL,"kmeleon.plugins.hotlist.chevron"));
setcheck("macros(OpenBook1)",getpref(STRING,$book)=="ID_OPEN_LINK" ? true:false);
setcheck("macros(OpenBook2)",getpref(STRING,$book)=="ID_OPEN_LINK_IN_NEW_WINDOW"? true:false);
setcheck("macros(OpenBook3)",getpref(STRING,$book)=="layers(OpenURL)"? true:false);
setcheck("macros(OpenBook4)",getpref(STRING,$book)=="layers(OpenURLBg)"? true:false);
setcheck("macros(OpenBook5)",getpref(STRING,$book)=="ID_OPEN_LINK_IN_BACKGROUND"? true:false);
setcheck("macros(OpenFav1)",getpref(STRING,$fav)=="ID_OPEN_LINK"? true:false);
setcheck("macros(OpenFav2)",getpref(STRING,$fav)=="ID_OPEN_LINK_IN_NEW_WINDOW"? true:false);
setcheck("macros(OpenFav3)",getpref(STRING,$fav)=="layers(OpenURL)"? true:false);
setcheck("macros(OpenFav4)",getpref(STRING,$fav)=="layers(OpenURLBg)"? true:false);
setcheck("macros(OpenFav5)",getpref(STRING,$fav)=="ID_OPEN_LINK_IN_BACKGROUND"? true:false);
setcheck("macros(OpenHot1)",getpref(STRING,$hot)=="ID_OPEN_LINK"? true:false);
setcheck("macros(OpenHot2)",getpref(STRING,$hot)=="ID_OPEN_LINK_IN_NEW_WINDOW"? true:false);
setcheck("macros(OpenHot3)",getpref(STRING,$hot)=="layers(OpenURL)"? true:false);
setcheck("macros(OpenHot4)",getpref(STRING,$hot)=="layers(OpenURLBg)"? true:false);
setcheck("macros(OpenHot5)",getpref(STRING,$hot)=="ID_OPEN_LINK_IN_BACKGROUND"? true:false);

setcheck("macros(Enumerate)",getpref(BOOL,$num));
setcheck("macros(Pressed)",getpref(INT,$style)==2? true:false);
setcheck("macros(Pressed+)",getpref(INT,$style)==10? true:false);
setcheck("macros(Buttons)",getpref(INT,$style)==6? true:false);
setcheck("macros(Buttons+)",getpref(INT,$style)==14? true:false);
setcheck("macros(SetCloseLast)",getpref(INT,$onClose)==0? true:false);
setcheck("macros(SetCloseLeft)",getpref(INT,$onClose)==1? true:false);
setcheck("macros(SetCloseRight)",getpref(INT,$onClose)==2? true:false);
setcheck("macros(SetOpenNext)",getpref(INT,$onOpen)==1? true:false);
setcheck("macros(SetOpenLast)",getpref(INT,$onOpen)==0? true:false);
setcheck("macros(ConfirmToggle)",getpref(BOOL,$confirm));

setcheck("macros(Gestures)",getpref(BOOL,$mouse."load"));

setcheck("macros(UA)",""==getpref(STRING,$ua)? true:false);
setcheck("macros(UA1)",getpref(STRING,$pu."1".$ps)==getpref(STRING,$ua)? true:false);
setcheck("macros(UA2)",getpref(STRING,$pu."2".$ps)==getpref(STRING,$ua)? true:false);
setcheck("macros(UA3)",getpref(STRING,$pu."3".$ps)==getpref(STRING,$ua)? true:false);
setcheck("macros(ResetUA)",getpref(BOOL,$uar)?false:true);

getpref(BOOL,$layers)==false ? setpref(BOOL,$catch,false):"";
getpref(BOOL,$layers)==false ? setpref(STRING,$mode,"WINDOWS ONLY"):(getpref(BOOL,$catch)==true ? setpref(STRING,$mode,"LAYERS ONLY"):setpref(STRING,$mode,""));
setcheck("macros(LayersOnly)",getpref(BOOL,$catch)==true ? true:false);
setcheck("macros(LayersWindows)",getpref(STRING,$mode)==""? true:false);
setcheck("macros(WindowsOnly)",$layer==false ? true:false);

setcheck("macros(URLInCurr)",getpref(STRING,$ou)=="ID_OPEN_LINK"? true:false);
setcheck("macros(URLInNew)", getpref(STRING,$ou)=="ID_OPEN_LINK_IN_NEW_WINDOW"? true:false);
setcheck("macros(URLInLayer)",getpref(STRING,$ou)=="layers(OpenURL)"? true:false);
setcheck("macros(URLInBg)",getpref(STRING,$ou)=="ID_OPEN_LINK_IN_BACKGROUND"? true:false);
setcheck("macros(URLInBgLayer)",getpref(STRING,$ou)=="layers(OpenURLBg)"? true:false);

setcheck("macros(BlockScripts)",getpref(BOOL,$flip));
setcheck("macros(AllowScripts)",getpref(BOOL,$flip)==false ? true:false);

setcheck("macros(AniToggle)",getpref(STRING,$animation)=="none"? true:false);
pluginmsg(toolbars,"CheckButton","Privacy Bar,macros(AniToggle),".(getpref(STRING,$animation)=="none"? 1:0));

setcheck("macros(CToggle)",getpref(BOOL,$colors)? false:true);
pluginmsg(toolbars,"CheckButton","Privacy Bar,macros(CToggle),".(getpref(BOOL,$colors)? 0:1));

setcheck("macros(CKToggle)", getpref(INT,$cookies));
pluginmsg(toolbars,"CheckButton","Privacy Bar,macros(CKToggle),".(getpref(INT,$cookies) ? 1:0));

setcheck("macros(ImgToggle)",getpref(INT,$images));
pluginmsg(toolbars,"CheckButton","Privacy Bar,macros(ImgToggle),".(getpref(INT,$images)? 1:0));

setcheck("macros(JToggle)", getpref(BOOL,$java)? false:true);
pluginmsg(toolbars,"CheckButton","Privacy Bar,macros(JToggle),".(getpref(BOOL,$java)? 0:1));

setcheck("macros(JSToggle)",getpref(BOOL,$jscript)? false:true);
pluginmsg(toolbars,"CheckButton","Privacy Bar,macros(JSToggle),".(getpref(BOOL,$jscript)? 0:1));

setcheck("macros(PBToggle)",getpref(BOOL,$popups));
pluginmsg(toolbars,"CheckButton","Main Bar,macros(PBToggle),".(getpref(BOOL,$popups)? 1:0));
pluginmsg(toolbars,"CheckButton","Privacy Bar,macros(PBToggle),".(getpref(BOOL,$popups)? 1:0));

setcheck("macros(RefToggle)",getpref(INT,$referer)? false:true);
pluginmsg(toolbars,"CheckButton","Privacy Bar,macros(RefToggle),".(getpref(INT,$referer)? 0:1));

}


# ----CONFIG MACROS----

$skindir=getpref(STRING,"kmeleon.general.skinsDir");
$skin=getpref(STRING,"kmeleon.general.skinsCurrent");

Commands{
exec("notepad.exe ".$skindir."commands.txt");
}
Toolbars{
exec("notepad.exe ".$skindir.$skin."toolbars.cfg");
}
Macros{
exec("notepad.exe ".$path."macros.cfg");
}
Menus{
exec("notepad.exe ".$path."menus.cfg");
}
Accel{
exec("notepad.exe ".$path."accel.cfg");
}
Prefs{
exec("notepad.exe ".$path."prefs.js");
}
User{
exec("notepad.exe ".$path."user.js");
}
UserContent{
exec("notepad.exe ".$path."\chrome\\userContent.css");
}

JSConsole{
open("javascript:");
}

XUL{
$layer==true ? pluginmsg(layers,OpenURL,$xul):opennew($xul);
}
AllPrefs{
$xul="about:config"; &XUL;
}
RSS{
$xul="chrome://aggreg8/content/aggreg8.xul"; &XUL;
}
Master{
$xul="chrome://pippki/content/pref-masterpass.xul"; &XUL;
}
Popups{
$xul="chrome://communicator/content/popupManager.xul"; &XUL;
}
Cert{
$xul="chrome://pippki/content/certManager.xul"; &XUL;
}
Cookies{
$xul="chrome://cookie/content/pref-cookies.xul"; &XUL; $loaded="CookMgr"; 
}
CookMgr{
open("javascript:viewCookies()"); $loaded="";
}
Images{
$xul="chrome://cookie/content/pref-images.xul"; &XUL; $loaded="ImgMgr";
}
ImgMgr{
open("javascript:viewImages()"); $loaded="";
}


# ----MISC. MACROS----

SetHome{
setpref(STRING,"kmeleon.general.homePage",$URL); 
setpref(INT,$sg,0); setpref(BOOL,$sh,true); &Sync;
alert("'".$TITLE."'  is now your Home Page.","Home Page Updated",INFO);
}

KMWiki{
open("http://kmeleon.sourceforge.net/wiki/index.php?id=Welcome");
}

LinkInfo {
confirm($LinkURL."\n\n\nDo you want to go to this address now?","URL OF SELECTED LINK:",YESNO)=="YES"? open($LINKURL):"";
}

ListLinks{
getpref(BOOL,$popups)==true ? &PB:&ViewLinks; 
}
PB{
&PBToggle; &ViewLinks; &PBToggle;
}
ViewLinks{
$macro="OpenLinks"; &JSEnable;
}
OpenLinks{
open("javascript:LnkW=open('','LnkW','width=600,height=300,scrollbars,resizable');sLFw=window.name;if(!sLFw){sLFw=window.name='sLFw'};sLFd=document;DlnK=document.links;with(LnkW.document){write('<base target='+sLFw+'>'+DlnK.length+' total links from:<br><b>'+sLFd.title+'</b><br>'+sLFd.URL.link(sLFd.URL)+'<hr>');for(lNK=0;lNK<DlnK.length;lNK++){write('<p>'+DlnK[lNK].text+'<br>'+DlnK[lNK].href.link(DlnK[lNK]))};close();LnkW.focus()}") ;
}

FrameInLayer{
pluginmsg(layers,OpenURL,$FRAMEURL);
}

FrameInBack{
$layer==true ? pluginmsg(layers,OpenURLBg,$FRAMEURL):OpenBg($FRAMEURL);
}

ReloadFrame {
$macro="ReloadFrm"; &JSEnable;
}
ReloadFrm {
$FrameURL=""; $PageURL=$FrameURL;
open("javascript: function chkframes(elem) { var rep; if (unescape(elem.location) == '".$PageURL."' || '".$PageURL."'=='') {  return elem; } for (var f=0; f<elem.frames.length; f++) { rep=chkframes(elem.frames[f]); if (rep) return rep; } return; }  function reloadFrame() { var elem=chkframes(window); elem.location.reload(); } void(reloadFrame()); ");");
}

AutoDetect {
setpref(STRING,"intl.charset.detector",$ARG); forcecharset();
}

ForceCharset {
forcecharset($ARG);
}

$ie="\"C:\\Program Files\\Internet Explorer\\iexplore.exe\"";

URLInIE{
$oldclip=getclipboard(); setclipboard(); 
id(ID_SELECT_URL); id(ID_EDIT_COPY); $theurl=getclipboard();
$theurl==""? $theurl=$URL:""; 
&ResetURL; setclipboard($oldclip);
exec($ie.$theurl);
}
LinkInIE{
exec($ie.$LINKURL);
}

Default{
exec("\SetDefault.exe ");
}

Continuous{
getpref(BOOL,$jscript)==false ? (confirm("JavaScript is currently disabled. It must be enabled to use this feature.\n\n\nEnable JavaScript and continue?","JavaScript Disabled",YESNO,EXCLAIM)=="YES" ? &JS1st:""):&Reload;
} 
JS1st{
&JSToggle; &Reload;
}
Reload{
open("javascript:(function(){var intv=prompt('Enter number of seconds between reloads. (Click Back button to stop)');if(intv&&!isNaN(intv)){with(document){write('<html><frameset rows=\"*,22\" framespacing=0 border=0 frameborder=no><frame noresize frameborder=no><frame scrolling=no noresize frameborder=no></frameset></html>');frames[0].location.href=document.location.href;var docstr='<html><body bgcolor=cyan style=\"margin-top:0px\">\n';docstr+='Reloading in <span id=\"caption\"></span>secs.</body>\n';docstr+='<script>\nvar reloadIntv='+intv+';\nvar secsLeft='+intv+';\nfunction reloadFrame(){secsLeft=reloadIntv+1;parent.frames[0].location.href=parent.frames[0].location.href;}\nfunction countDown(){secsLeft--;showTime();}\nfunction showTime(){document.getElementById(\"caption\").innerHTML=parseInt(secsLeft)+\" \";}\nsetInterval(\"reloadFrame()\",'+intv*1000+');\nsetInterval(\"countDown()\",1000);\nshowTime();\n</script>\n</html>';frames[1].document.write(docstr);}}})()");
}
