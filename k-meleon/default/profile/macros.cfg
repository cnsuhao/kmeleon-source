#
# Macro Definition File for K-Meleon
#
# Jeff Doozan <jeff@tcbnetworks.com>
#
#
# Available commands:
#
#  menu =
#    Set the text to be displayed in menus
#
#  open(url)
#    Open url in the current browser window
#
#  opennew(url)
#    Open url in new browser window
#
#  openbg(url)
#    Open url in new background window
#
#  exec(command parameters)
#    Execute an external command
#
#  setpref(type, pref, value)
#    Set a preference
#    type can be BOOL, INT, or STRING
#
#  togglepref(type, pref, values...)
#    rotate a preference between a series of values
#    ex.:  togglepref(INT, "kmeleon.test.value", 17, 37, 42);
#    note that type BOOL only takes two arguments
#    ex.:  togglepref(BOOL, "javascript.enabled");
#
#  id(identifier)
#    send a message id to the current window
#    ex.:  id(ID_FILE_PRINT);
#
#  plugin(file, command)
#    run a plugin command
#    ex.: plugin(bookmarks, Add);
#
#
# Sample Usage:
#
#   in accel.cfg
#      KEY = macros(example)
#
#   in menus.cfg
#      macros(example)
#
#   in macros.cfg
#      example {
#         # String to be displayed in the menus
#            menu = &Kmeleon Example Macro;
#
#         # open the K-Meleon webpage in the current window
#            open("www.kmeleon.org");
#
#         #open the forums in a new background window
#            openbg("www.kmeleon.org/forum/");
#      }
#

fontsize_up {
    menu = "Larger Fonts";
    $newsize = getpref(INT, "font.size.variable.x-western") + 1;
    setpref(INT, "font.size.variable.x-western", $newsize);
    statusbar("Font size set to " . $newsize);
}

fontsize_down {
    menu = "Smaller Fonts";
    $newsize = getpref(INT, "font.size.variable.x-western") - 1;
    setpref(INT, "font.size.variable.x-western", $newsize);
    statusbar("Font size set to " . $newsize);
}

fish {
	open("home.netscape.com/fishcam/fishcam.html");
}


go_button {
    $oldclip = getclipboard();
    id(ID_SELECT_URL);
    id(ID_EDIT_COPY);
    $theurl = getclipboard();
    setclipboard($oldclip);
    open($theurl);
}


up_directory {
    open( dirname( $URL ) );
}


$dictionary = "http://dict.org/bin/Dict?Form=Dict2&Database=*&Query=";
# $dictionary = "http://www.m-w.com/cgi-bin/dictionary?";
# $dictionary = "http://www.dictionary.com/search?q=";
# $dictionary = "http://www.wikipedia.org/w/wiki.phtml?search=";

search_dictionary {
    menu = "Search Dictionary...";
    $word = prompt("Enter search query:", "Dictionary Search");
    $word == "" ? "" : open( $dictionary . $word );
}
